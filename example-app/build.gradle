apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.djodjo.tarator.example"
        testApplicationId "org.djodjo.tarator.example.test"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner getInstrumentationTestRunner();
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }
}



def getInstrumentationTestRunner() {

    //default
    def result = "org.djodjo.tarator.runner.TaratorJunitRunner";

    if (project.hasProperty("testType")) {

        switch (project.getProperties().get("testType")) {

            case "g-validate":
                result = "org.djodjo.tarator.example.instr.ValidateInstr";
                break;

            case "g-unit":
                result = "org.djodjo.tarator.example.instr.UnitInstr";
                break;

            case "g-regression":
                result = "org.djodjo.tarator.example.instr.RegressionInstr";
                break;

            case "junit":
                result = "org.djodjo.tarator.runner.TaratorJunitRunner";
                break;
        }
    }

    logger.lifecycle("Using TestRunner: " + result)

    return result
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile project(':runner')

    compile 'com.android.support:appcompat-v7:21.0.3'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    //androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
    //    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.0'
    androidTestCompile project(':core')
    androidTestCompile 'info.cukes:cucumber-android:1.2.2'
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.2'
    compile 'com.android.support:support-annotations:21.0.3'

}
