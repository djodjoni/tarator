apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.djodjo.tarator.example"
        testApplicationId "org.djodjo.tarator.example.test"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner getInstrumentationTestRunner();
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }
}


def getInstrumentationTestRunner() {

    //default
    def result = "org.djodjo.tarator.runner.TaratorJunitRunner";

    //when for example: ../gradlew connectedAndroidTest -PtestType=unit
    if (project.hasProperty("testType")) {

        switch (project.getProperties().get("testType")) {

            case "new":
                result = "org.djodjo.tarator.example.instr.ValidateInstr";
                break;

            case "unit":
                result = "org.djodjo.tarator.example.instr.UnitInstr";
                break;

            case "all":
                result = "org.djodjo.tarator.example.instr.RegressionInstr";
                break;

            case "junit":
                result = "org.djodjo.tarator.runner.TaratorJunitRunner";
                break;
        }
    }

    logger.lifecycle("Using TestRunner: " + result)

    return result
}

dependencies {

    androidTestCompile project(':spoon')
    androidTestCompile project(':support-v4')
    androidTestCompile project(':support-v7')
    //androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    //androidTestCompile 'com.android.support.test.espresso:espresso-core:2.1'
    androidTestCompile(project(':yogurt'))

    androidTestCompile(project(':cucumber'))

    androidTestCompile('info.cukes:cucumber-android:1.2.2') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    androidTestCompile 'info.cukes:cucumber-picocontainer:1.2.2'


    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.4.0'

    //compile 'com.android.support.test.espresso:espresso-contrib:2.1'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:gridlayout-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:support-annotations:22.2.0'
    compile 'com.google.guava:guava:18.0'
    compile 'org.djodjo.comm:jus:0.3.3'
    compile 'org.djodjo.json:json-core:1.+'
    compile 'org.djodjo.json:json-wrapper:1.+'

    compile 'com.google.android.gms:play-services:7.5.0'

}
